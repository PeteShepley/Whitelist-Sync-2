description = "WhitelistSyncCore"

apply plugin: 'eclipse'

eclipse {
    project {
        name = "WhitelistSync(WhitelistSyncCore)"
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

configurations {
    shade
    implementation.extendsFrom shade
    cleanedAnnotations
    implementation.exclude group: 'org.jetbrains' , module:'annotations'
    implementation.exclude group: 'org.slf4j'
    implementation.exclude group: 'com.google'
}

dependencies {
    compileOnly "com.google.code.gson:gson:2.8.9"
    shade 'com.squareup.okhttp3:okhttp:[4.12.0,5.0)'
    shade 'com.microsoft.signalr:signalr:7.0.0'
}

jar {
    archiveClassifier = 'unshaded'
}

var shadeRelocateGroup = 'net.rmnad.shade'

shadowJar {
    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
        exclude(dependency('org.slf4j::'))
    }
    relocate('okhttp3', "${shadeRelocateGroup}.okhttp3")
    relocate('com.microsoft', "${shadeRelocateGroup}.com.microsoft")
    relocate('okio', "${shadeRelocateGroup}.okio")
    relocate('org.json', "${shadeRelocateGroup}.org.json")
    relocate('kotlin', "${shadeRelocateGroup}.kotlin")
    relocate('com.google.gson', "${shadeRelocateGroup}.com.google.gson")
    relocate('io.reactivex', "${shadeRelocateGroup}.io.reactivex")
    relocate('org.reactivestreams', "${shadeRelocateGroup}.org.reactivestreams")

    archiveBaseName = "WhitelistSyncCore"
    archiveClassifier = ''
    destinationDirectory = file '../target'
    configurations = [project.configurations.shade]
}

artifacts {
    archives shadowJar
    shadow shadowJar
}